plugins {
    id 'xyz.jpenilla.run-paper' version '2.3.1'
    id 'io.papermc.paperweight.userdev' version '2.0.0-beta.19'
}

// Paper dev-bundle sometimes pulls deps with empty version; force versions for resolution
configurations.configureEach {
    resolutionStrategy.eachDependency { details ->
        if (details.requested.group == 'net.kyori' && details.requested.name == 'adventure-text-serializer-ansi'
                && (!details.requested.version || details.requested.version.isEmpty())) {
            details.useVersion '4.25.0'
            details.because 'Paper dev-bundle omits version'
        }
    }
}

dependencies {
    implementation project(':bukkit')
    compileOnly project(':common')

    paperweight.paperDevBundle('1.21.11-R0.1-SNAPSHOT')

    compileOnly 'org.jetbrains:annotations:26.0.2'
    compileOnly 'org.projectlombok:lombok:1.18.38'
    compileOnly 'net.william278.cloplib:cloplib-bukkit:2.0.11'
}

tasks.named('assemble') {
    dependsOn(tasks.named('reobfJar'))
}

shadowJar {
    dependencies {
        exclude(dependency('com.mojang:brigadier'))
    }

    relocate 'org.apache.commons.io', 'net.william278.husktowns.libraries.commons.io'
    relocate 'org.apache.commons.text', 'net.william278.husktowns.libraries.commons.text'
    relocate 'org.apache.commons.lang3', 'net.william278.husktowns.libraries.commons.lang3'
    relocate 'de.themoep', 'net.william278.husktowns.libraries'
    relocate 'org.jetbrains', 'net.william278.husktowns.libraries'
    relocate 'org.intellij', 'net.william278.husktowns.libraries'
    relocate 'com.zaxxer', 'net.william278.husktowns.libraries'
    relocate 'de.exlll', 'net.william278.husktowns.libraries'
    relocate 'net.william278.paginedown', 'net.william278.husktowns.libraries.paginedown'
    relocate 'net.william278.desertwell', 'net.william278.husktowns.libraries.desertwell'
    relocate 'net.william278.cloplib', 'net.william278.husktowns.libraries.cloplib'
    relocate 'net.william278.toilet', 'net.william278.husktowns.libraries.toilet'
    relocate 'org.json', 'net.william278.husktowns.libraries.json'
    relocate 'dev.dejvokep.boostedyaml', 'net.william278.husktowns.libraries.boostedyaml'
    relocate 'org.yaml.snakeyaml', 'net.william278.husktowns.libraries.snakeyaml'
    relocate 'com.google.gson', 'net.william278.husktowns.libraries.gson'
    relocate 'org.bstats', 'net.william278.husktowns.libraries.bstats'
    relocate 'me.lucko', 'net.william278.husktowns.libraries'
    relocate 'net.roxeez', 'net.william278.husktowns.libraries'

    minimize()
}

tasks {
    runServer {
        minecraftVersion('1.21.11')
    }
}